plugins {
    id "java"
    id "edu.wpi.first.GradleRIO" version "2019.4.1"
}

group = 'com.github.team6479'

sourceCompatibility = JavaVersion.VERSION_11
targetCompatibility = JavaVersion.VERSION_11

repositories {
    mavenCentral()
    maven {
         url  "http://first.wpi.edu/FRC/roborio/maven/release/"
    }
}

dependencies {
    compile wpi.deps.wpilib()
    compile wpi.deps.vendor.java()
    nativeZip wpi.deps.vendor.jni(wpi.platforms.roborio)
    nativeDesktopZip wpi.deps.vendor.jni(wpi.platforms.desktop)
}

test {
    testLogging {
        events "passed", "skipped", "failed"
    }
}

task sourceJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

task cleanGitHubDocs(type: Delete) {
    delete "docs"
}

task genGitHubDocs(type: Javadoc, dependsOn: cleanGitHubDocs) {
    source = sourceSets.main.allJava
    classpath = configurations.compile
    title = "FRC Team 6479 Robot Library"
    destinationDir = new File("${rootDir}/docs")
}

artifacts {
    archives sourceJar, javadocJar
}

wrapper {
    gradleVersion = "5.4.1"
}
