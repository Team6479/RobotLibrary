plugins {
    id "java"
    id "java-library"
    id "eclipse"
    id "idea"
    id "maven"
    id "maven-publish"
    id "com.jfrog.bintray" version "1.7.3"
}

repositories {
    mavenCentral()
    maven {
         url  "http://first.wpi.edu/FRC/roborio/maven/release/"
    }
}

dependencies {

    //Test Deps
    testCompile group: 'junit', name: 'junit', version: '4.12'

    //FRC Deps
    compile group: 'edu.wpi.first.ntcore', name: 'ntcore-java', version: '4.0.0'
    compile group: 'edu.wpi.first.wpilibj', name: 'wpilibj-java', version: '2018.1.1'
    compile group: 'edu.wpi.first.wpiutil', name: 'wpiutil-java', version: '3.0.0'
}

test {
    testLogging {
        events "passed", "skipped", "failed"
    }
}

task sourceJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task packageJavadoc(type: Jar) {
    from javadoc
    classifier = 'javadoc'
}

javadoc << {
    source = sourceSets.main.allJava
    classpath = configurations.compile
    title = "FRC Team 6479 Robot Library"
    destinationDir = new File("${rootDir}/docs")
}


artifacts {
    archives sourceJar, packageJavadoc
}

tasks.withType(Zip) { task ->
    task.doLast {
        ant.checksum file: it.archivePath
    }
}

publishing {
    publications {
        robotlib(MavenPublication) {
            from components.java

            groupId 'team6479.robotrepo'
            artifactId "robotlib"
            version projectVersion

            artifact sourceJar
            artifact packageJavadoc
        }
    }
}

bintray {
    user = bintrayUser
    key = bintrayKey
    publications = ['robotlib']

    dryRun = false
    publish = true
    override = false

    pkg {
        repo = 'RobotRepo'
        name = 'robotlib'
        userOrg = 'team6479'
        version {
            name = projectVersion
            desc = "Robotlib release ${projectVersion}"
        }
    }
}

task wrapper(type: Wrapper) {
    gradleVersion = '4.5'
}
